import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'java'
	id 'application'
	id 'com.github.johnrengelman.shadow' version '6.0.0'
	id 'antlr'
}

repositories {
	mavenCentral()
	maven {
		name 'm2-dv8tion'
		url 'https://m2.dv8tion.net/releases'
	}

	maven { url 'https://jitpack.io' }
	
	jcenter()
}

shadowJar {
	archiveBaseName = 'Sx4'
	archiveClassifier = ''
	archiveVersion = ''
}

mainClassName = 'com.sx4.bot.core.Sx4'

sourceCompatibility = JavaVersion.VERSION_15
targetCompatibility = JavaVersion.VERSION_15

project.ext.parserPackage = 'com.sx4.bot.antlr'
sourceSets.main.java.srcDirs = ['src/main/java', 'generated-src/main/java']

dependencies {
	implementation('com.github.21Joakim:Jockie-Utils:96c51b29f3') {
		exclude group: 'net.dv8tion', module: 'JDA'
	}

	implementation('com.github.21Joakim:JDA-Memory-Optimizations:+') {
		exclude group: 'net.dv8tion', module: 'JDA'
	}
	
	compile 'club.minnced:discord-webhooks:0+'

	compile 'com.vdurmont:emoji-java:5+'
	
	compile group: 'org.jsoup', name: 'jsoup', version: '1.11.3'

	implementation('net.dv8tion:JDA:4+') {
		exclude module: 'opus-java'
	}
	
	implementation 'org.mongodb:mongodb-driver-sync:+'

	implementation group: 'org.postgresql', name: 'postgresql', version: '42+'
	implementation "com.zaxxer:HikariCP:4.0.3"

	implementation 'net.sf.trove4j:trove4j:3.0.3'
	
	compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.8'

	compile group: 'org.codehaus.groovy', name: 'groovy', version: '3+'
	
	compile group: 'org.glassfish.jersey.core', name: 'jersey-server', version: '2.28'
	compile group: 'org.glassfish.jersey.containers', name: 'jersey-container-servlet', version: '2.28'
	compile group: 'org.glassfish.jersey.inject', name: 'jersey-hk2', version: '2.28'
	compile group: 'org.glassfish.jersey.media', name: 'jersey-media-json-jackson', version: '2.28'
	compile group: 'org.glassfish.jersey.ext', name: 'jersey-mvc-jsp', version: '2.28'
	
	compile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.14.v20181114'
	compile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.14.v20181114'
	
	compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.4.0-b180830.0359'
	
	compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.6.2'

	antlr 'org.antlr:antlr4:4.7.2'
}

def getGitHash = { ->
	def stdout = new ByteArrayOutputStream()
	exec {
		commandLine 'git', 'rev-parse', '--short', 'HEAD'
		standardOutput = stdout
	}
	return stdout.toString().trim()
}

def sourcesForRelease = task sourcesForRelease(type: Copy) {
	delete("build/filteredSrc")

	from("src/main/java") {
		include("**/Sx4.java")
		filter(ReplaceTokens, tokens: ["GIT_HASH": getGitHash()])
	}

	into("build/filteredSrc")
	includeEmptyDirs = false
}

def generateJavaSources = task generateJavaSources(type: SourceTask) {
	def javaSources = sourceSets["main"].allJava.filter {
		it.name != "Sx4.java"
	}.asFileTree

	source = javaSources + fileTree(sourcesForRelease.destinationDir)

	dependsOn(sourcesForRelease)
}

generateGrammarSource {
	outputs.cacheIf { true }
	source = 'src/main/antlr'
	arguments += ['-visitor', '-no-listener', '-package', parserPackage, '-Xexact-output-dir']

	doLast {
		println 'Copying generated grammar lexer/parser files to source'
		mkdir 'generated-src/main/java'
		copy {
			from generateGrammarSource.outputDirectory.canonicalPath
			into "generated-src/main/java/${parserPackage.replace('.', '/')}"
		}
		delete "$buildDir/generated-src/antlr"
	}
}

compileJava {
	source = generateJavaSources.source
	dependsOn generateJavaSources
	options.encoding = "UTF-8"
}
